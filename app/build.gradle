apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.yly.androidallinone"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        flavorDimensions "yly"

        ndk {
            abiFilters "x86", "armeabi-v7a"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters "armeabi-v7a"
            }
        }

        debug {
            ndk {
                abiFilters "x86", "armeabi-v7a"
            }
        }

        applicationVariants.all {
            variant ->
                variant.outputs.all {
                    //打包文件名格式
                    outputFileName = "yf_${defaultConfig.versionName}_${releaseTime()}.apk"
                }
        }
    }

    productFlavors {
        prod {
            dimension "yly"
        }
        mock {
            dimension "yly"
        }
    }

    lintOptions {
        //To make sure that your Java code interoperates well with your Kotlin code
        check 'Interoperability'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled true
    }
}

configurations.all {
    resolutionStrategy {
//        force 'androidx.documentfile:documentfile:1.0.0'
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'androidx.databinding:databinding-adapters:3.2.1'
    // 要与compiler匹配使用，均使用最新版可以保证兼容
    implementation 'com.alibaba:arouter-api:1.4.1'
    kapt 'com.alibaba:arouter-compiler:1.2.1'
    implementation project(':rootertesta')
    implementation project(':routertestb')
    implementation project(':commonimp')
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

repositories {
    flatDir {
        dirs 'libs'   // aar目录
    }
}

androidExtensions {
    experimental = true
    defaultCacheImplementation = "HASH_MAP" // also SPARSE_ARRAY, NONE
}
